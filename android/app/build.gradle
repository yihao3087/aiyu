plugins {
    id "com.android.application"
    id "dev.flutter.flutter-gradle-plugin"
    id "org.jetbrains.kotlin.android"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keyProperties = new Properties()
def keyPropertiesFile = rootProject.file('android/key.properties')
if (keyPropertiesFile.exists()) {
    keyPropertiesFile.withReader('UTF-8') { reader ->
        keyProperties.load(reader)
    }
}

def envOrProp = { String key ->
    def value = project.findProperty(key)?.toString()
    if (value == null || value.trim().isEmpty()) {
        value = localProperties.getProperty(key)
    }
    if (value == null || value.trim().isEmpty()) {
        value = keyProperties.getProperty(key)
    }
    if (value == null || value.trim().isEmpty()) {
        value = System.getenv(key)
    }
    return value?.trim()
}

def placeholderValue = { String key ->
    return envOrProp(key) ?: ""
}

def releaseStoreFile = envOrProp('RELEASE_STORE_FILE')
def releaseStorePassword = envOrProp('RELEASE_STORE_PASSWORD')
def releaseKeyAlias = envOrProp('RELEASE_KEY_ALIAS')
def releaseKeyPassword = envOrProp('RELEASE_KEY_PASSWORD')
def hasReleaseKeystore = [
        releaseStoreFile,
        releaseStorePassword,
        releaseKeyAlias,
        releaseKeyPassword
].every { it != null && !it.isEmpty() }

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.zucuzu.chuanxun"
    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    // Plugins still declare ndk.abiFilters; keep Gradle splits disabled to avoid conflicts.
    splits {
        abi {
            enable false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.zucuzu.chuanxun"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdkVersion 24
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        multiDexEnabled true
        ndk {
            abiFilters 'arm64-v8a'
        }

        manifestPlaceholders = [
                GETUI_APPID    : placeholderValue("GETUI_APPID"),
                GETUI_APPKEY   : placeholderValue("GETUI_APPKEY"),
                GETUI_APPSECRET: placeholderValue("GETUI_APPSECRET"),
                XIAOMI_APP_ID  : "",
                XIAOMI_APP_KEY : "",
                MEIZU_APP_ID   : "",
                MEIZU_APP_KEY  : "",
                HUAWEI_APP_ID  : "",
                OPPO_APP_KEY   : "",
                OPPO_APP_SECRET: "",
                VIVO_APP_ID    : "",
                VIVO_APP_KEY   : "",
                HONOR_APP_ID   : "",
                AMAP_ANDROID_KEY: placeholderValue("AMAP_ANDROID_KEY"),
        ]

        lintOptions {
            disable 'InvalidPackage'
            disable "Instantiatable"
            checkReleaseBuilds false
            abortOnError false
        }
    }

    signingConfigs {
        debug {
            storeFile file("${System.properties['user.home']}/.android/debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            if (hasReleaseKeystore) {
                storeFile file(releaseStoreFile)
                storePassword releaseStorePassword
                keyAlias releaseKeyAlias
                keyPassword releaseKeyPassword
            }
        }
    }

buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        profile {
            signingConfig hasReleaseKeystore ? signingConfigs.release : signingConfigs.debug

        }
        release {
//            minifyEnabled false
//            useProguard false
//            shrinkResources false
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig hasReleaseKeystore ? signingConfigs.release : signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

flutter {
    source '../..'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.media:media:1.6.0'

    implementation 'com.getui:gtsdk:3.3.12.0'
    implementation 'com.getui:gtc:3.2.18.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
}

static def buildTime() {
    return new Date().format("MMddHHmm", TimeZone.getTimeZone("GMT+08:00"))
}

