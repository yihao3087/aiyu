import groovy.xml.XmlSlurper

allprojects {
    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://maven.aliyun.com/nexus/content/repositories/jcenter' }
        maven { url 'https://maven.aliyun.com/nexus/content/repositories/google' }
        maven { url 'https://maven.aliyun.com/nexus/content/repositories/gradle-plugin' }
        maven { url "https://dl.google.com/dl/android/maven2/" }   // fcm
        maven { url 'https://developer.huawei.com/repo/' }
        maven { url 'https://mvn.getui.com/nexus/content/repositories/releases/' }
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

def setNamespaceFromManifest = { project ->
    def androidExt = project.extensions.findByName("android")
    if (androidExt != null) {
        def currentNamespace = null
        if (androidExt.hasProperty("namespace")) {
            currentNamespace = androidExt.namespace
        }
        if (!currentNamespace) {
            def manifestFile = project.file("src/main/AndroidManifest.xml")
            if (manifestFile.exists()) {
                def manifest = new XmlSlurper(false, false).parse(manifestFile)
                def manifestPackage = manifest.@package?.text()
                if (manifestPackage) {
                    androidExt.namespace = manifestPackage
                }
            }
        }
    }
}

subprojects { subproject ->
    subproject.plugins.withId("com.android.application") {
        setNamespaceFromManifest(subproject)
    }
    subproject.plugins.withId("com.android.library") {
        setNamespaceFromManifest(subproject)
    }
}
